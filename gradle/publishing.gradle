apply plugin: "maven-publish"
apply plugin: 'com.jfrog.bintray'

ext.isSnapshot = version.endsWith("SNAPSHOT")

task sourceJar(type: Jar) {
	classifier "sources"
	from sourceSets.main.allJava
}

if (isSnapshot) {
	publishing {
		publications {
			main(MavenPublication) {
				from components.java
				artifact sourceJar
			}
		}
		if (project.hasProperty('smartThingsArtifactoryUserName')) {
			repositories {
				maven {
					credentials {
						username smartThingsArtifactoryUserName
						password smartThingsArtifactoryPassword
					}
					url "https://artifactory.smartthings.com/libs-snapshot-local"
				}
			}
		}
	}
} else {
	publishing {
		publications {
			mavenJava(MavenPublication) {
				from components.java
				artifact sourceJar
				pom.withXml {
					def root = asNode()
					// Work around gradle bug that publishes compile dependencies as runtime
					// http://discuss.gradle.org/t/maven-publish-plugin-generated-pom-making-dependency-scope-runtime/7494/10
					root.dependencies.'*'.findAll() {
						it.scope.text() == 'runtime' && project.configurations.compile.allDependencies.find { dep ->
							dep.name == it.artifactId.text()
						}
					}.each() {
						it.scope*.value = 'compile'
					}
				}
			}
		}
	}
}

bintrayUpload { task ->
	doFirst {
		if (isSnapshot) {
			throw new GradleException('Cannot publish SNAPSHOT versions to BinTray!')
		}
	}
	gradle.taskGraph.whenReady { taskGraph ->
		if (taskGraph.hasTask(task)) {
			task.user = System.getenv('BINTRAY_USER') ?: ''
			task.apiKey = System.getenv('BINTRAY_API_KEY') ?: ''
		}
	}
}

bintray {
	publications = ['mavenJava']
	dryRun = false
	publish = true
	pkg {
		repo = 'maven'
		name = 'smartthings.cassandra-migration'
		userOrg = 'smartthingsoss'
		licenses = ['Apache-2.0']
		desc = 'Auto migration of cassandra schema'
		labels = ['cassandra']
		websiteUrl = 'https://github.com/SmartThingsOSS/cassandra-migration'
		issueTrackerUrl = 'https://github.com/SmartThingsOSS/cassandra-migration/issues'
		vcsUrl = 'https://github.com/SmartThingsOSS/cassandra-migration.git'
		version {
			name = project.version
			vcsTag = project.version
		}
	}
}
