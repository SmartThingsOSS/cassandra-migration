buildscript {
	repositories {
		jcenter()
		mavenCentral()
		mavenLocal()
	}

	dependencies {
		classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.1'
	}
}

group = "smartthings"
version = file('version.txt').text.trim()


apply plugin: 'java'
apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: "maven-publish"
apply plugin: "idea"

sourceCompatibility = "1.7"
targetCompatibility = "1.7"

repositories {
	mavenCentral()
}

dependencies {
	compile 'com.datastax.cassandra:cassandra-driver-core:2.1.6'
	compile 'com.google.guava:guava:18.0'
}

task run(dependsOn: 'classes', type: JavaExec) {
	main = 'st.migration.MigrationExecutor'
	systemProperties = [host: host, port: port, keyspace: keyspace, migrationPath: migrationsPath]
	classpath = sourceSets.main.runtimeClasspath
}

task markAll(dependsOn: 'classes', type: JavaExec) {
	main = 'st.migration.MigrationExecutor'
	systemProperties = [host: host, port: port, keyspace: keyspace, migrationPath: migrationsPath, handlerClass: 'MarkCompleteHandler']
	classpath = sourceSets.main.runtimeClasspath
}

task sourceJar(type: Jar) {
		classifier "sources"
		from sourceSets.main.allJava
}

publishing {
	publications {
		main(MavenPublication) {
			from components.java
			artifact sourceJar
		}
	}
	repositories {
		maven {
			credentials {
				username smartThingsArtifactoryUserName
				password smartThingsArtifactoryPassword
			}
			if (version.endsWith('SNAPSHOT')) {
				url "http://artifactory.smartthings.com/libs-snapshot-local"
			} else {
				url "http://artifactory.smartthings.com/libs-release-local"
			}
		}
	}
}
